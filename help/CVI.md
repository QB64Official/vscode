## CVI
---

### The CVI function decodes a 2-byte STRING generated by MKI$ (or read from a file) to INTEGER numeric values.

#### SYNTAX

`result% = CVI ( stringData$ )`

#### DESCRIPTION
* CV functions ( [CVD](./CVD.md) , [CVS](./CVS.md) , [CVI](./CVI.md) , [CVL](./CVL.md) , [CVDMBF](./CVDMBF.md) , [CVSMBF](./CVSMBF.md) ) are used to convert values encoded by MK$ functions ( MKD$ , MKS$ , MKI$ , MKL$ , MKDMBF$ , MKSMBF$ ).
* QB64 has [_CV](./_CV.md) and _MK$ functions which can also deal with extended data types .
* [INTEGER](./INTEGER.md) values can range from -32768 to 32767.
* Doesn't return [_UNSIGNED](./_UNSIGNED.md) values.


#### EXAMPLES
##### Example 1:
```vb
FIELD #1, 2 AS N$, 12 AS B$...
GET #1     'GET does not need a position or variable with successive FIELD buffer reads
Y = CVI(N$)
```
  
##### Example 2: How CVI converts the ASCII code values created by the MKI$ function.
```vb
SCREEN 12
DIM Q AS STRING * 1
Q = CHR$(34)
' create Print using templates to align the values returned
tmp1$ = "1st character code = ### * 1   =   ### "
tmp2$ = "2nd character code = ### * 256 = ##### "
tmp3$ = "                                 &  "
tmp4$ = "                     CVI Total = ##### "

DO
 COLOR 14: LOCATE 13, 20: INPUT "Enter an Integer from 1 to 32767(0 quits): ", number%
 IF number% < 1 THEN EXIT DO
 CLS
 ASCII$ = MKI$(number%)     ' create the 2 byte character string
 COLOR 11
 _PRINTSTRING (152, 240), "MKI$ creates 2 byte ASCII string: " + Q + ASCII$ + Q ' displays character(s)

 asc1% = ASC(ASCII$)        ' find the ASCII code values of each character
 asc2% = ASC(ASCII$, 2)     ' QB64 allows ASC to read specific characters in a string

 LOCATE 18, 20: PRINT USING tmp1$; asc1%; asc1%
 LOCATE 19, 20: PRINT USING tmp2$; asc2%; asc2% * 256
 LOCATE 20, 20: PRINT USING tmp3$; "-----"
 LOCATE 21, 20: PRINT USING tmp4$; asc1% + (256 * asc2%)
LOOP
SYSTEM
```
  


#### SEE ALSO
* Featured in our "Keyword of the Day" series
* MKD$ , MKI$ , MKS$ , MKL$ , MKDMBF$ , MKSMBF$
* [CVS](./CVS.md) , [CVD](./CVD.md) , [CVL](./CVL.md) , [CVSMBF](./CVSMBF.md) , [CVDMBF](./CVDMBF.md)
* [_CV](./_CV.md) , _MK$
